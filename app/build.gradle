apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def contextRoot = properties.getProperty('qr.context.root', "/wow/v1/dpwallet")

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "au.com.woolworths.village.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "1.0.5"

        manifestPlaceholders = [
            qr_scheme: properties.getProperty('qr.scheme'),
            qr_hostname: properties.getProperty('qr.hostname'),
            qr_port: properties.getProperty('qr.port'),
            qr_context_root: contextRoot,
            qr_pattern: "${contextRoot}/customer/qr/.*"
        ]

        buildConfigField 'String', 'API_CONTEXT_ROOT', "\"${contextRoot.toString()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    viewBinding {
        enabled = true
    }
}

ext {
    coroutines_version = "1.3.6"
    lifecycle_version = "2.2.0"
    activity_version = "1.1.0"
    app_center_version = '3.2.1'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation "androidx.activity:activity-ktx:$activity_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'com.google.android.material:material:1.1.0'

    implementation "com.microsoft.appcenter:appcenter-analytics:${app_center_version}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${app_center_version}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${app_center_version}"

    // TODO: Update to use repository
    implementation project(':okhttp-gson')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
